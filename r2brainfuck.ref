* Copyright 2024 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2024-06-14
* https://github.com/Aleksandr3Bocharov/r2brainfuck

*====================================================================
* r2brainfuck
*
* Программа "r2brainfuck" написана
* на языке программирования Рефал-2.
* http://www.refal.net/~belous/refal2-r.htm
*
* Программа "r2brainfuck" использует
* библиотеку расширения Рефал-2.
* https://github.com/Aleksandr3Bocharov/r2libext
*
* Программа "r2brainfuck" использует 
* библиотеку GTK3.
* https://docs.gtk.org/gtk3/
*
* Программа "r2brainfuck" интерпретирует код
* на языке Brainfuck из исходного файла.
*====================================================================

            START
            ENTRY Go
            EMPTY Next, Prev, Inc, Dec, Write, Read
* from r2libext
            EXTRN Map, MapAccum, DelAccum, LoadFile
* from r2brainfuckc
*           EXTRN PutChar, GetChar, GTKInit, OpenFileDialog, MessageBox
* from r2lib
            EXTRN Prout, Card, M1, P1, Symb, Numb, Br, Dg

* Вывод справки
Help        = <Prout 'The programm "r2brainfuck" interprets code ' +
            'on the language Brainfuck from source file.\n'>

* <Interpretator WN*> == 
* WN ::= S(/Next/ /Prev/ /Inc/ /Dec/ /Write/ /Read/)L | (WN*)
Interpretator +
    EP      = <InterpretatorAux <DoInterpretator EP () /0/ ()>>

InterpretatorAux +
    EC      =

DoInterpretator +
    (EB) SC (EA) +
            = (EB) SC (EA)
    /Next/ ER (EB) SC () +
            = <DoInterpretator ER (EB SC) /0/ ()>
    /Next/ ER (EB) SC (SN EA) +
            = <DoInterpretator ER (EB SC) SN (EA)>   
    /Prev/ ER () SC (EA) +
            = <DoInterpretator ER () /0/ (SC EA)>
    /Prev/ ER (EB SP) SC (EA) +
            = <DoInterpretator ER (EB) SP (SC EA)>   
    /Inc/ ER (EB) /255/ (EA) +
            = <DoInterpretator (EB) /0/ (EA)>
    /Inc/ ER (EB) SC (EA) +
            = <DoInterpretator (EB) <P1 SC> (EA)>     							
    /Dec/ ER (EB) /0/ (EA) +
            = <DoInterpretator (EB) /255/ (EA)>
    /Dec/ ER (EB) SC (EA) +
            = <DoInterpretator (EB) <M1 SC> (EA)>   
    /Write/ ER (EB) SC (EA) +
            = <Prout SC> +
            <DoInterpretator ER (EB) SC (EA)>												
    /Read/ ER (EB) SC (EA) +
            = <DoInterpretator ER (EB) <Numb <Card>> (EA)>					
    (EN) ER (EB) /0/ (EA) +
            = <DoInterpretator ER (EB) /0/ (EA)>
    (EN) ER (EB) SC (EA) +
            = <DoInterpretator (EN) ER +
            <DoInterpretator EN (EB) SC (EA)>> 

* <Parser E('><+-.,[]')T> == WN*
* WN ::= S(/Next/ /Prev/ /Inc/ /Dec/ /Write/ /Read/)L | (WN*)
Parser +
            =
    '>' ER  = /Next/ <Parser ER>
    '<' ER  = /Prev/ <Parser ER>
    '+' ER  = /Inc/ <Parser ER>
    '-' ER  = /Dec/ <Parser ER>
    '.' ER  = /Write/ <Parser ER>
    ',' ER  = /Read/ <Parser ER>
    '[' ER  = <ParserAux <Parser ER>>
    ']' ER  = (ER)      

ParserAux +
    EP (ER) = (EP) <Parser ER>

* <Tokenizer (SS S(N)R S(N)C)*> == E('><+-.,[]')T | 'Fails' (V(O)E)+
Tokenizer +
    ES      = <TokenizerAux  +
            <Map /DoTokenizer/ ES> (<Dg 'Fails11'> <Dg 'Fails12'>)>

TokenizerAux +
    ET ()   = ET
    ET (EE) = 'Fails' EE
  
DoTokenizer +
    ('[' SR SC) +
            = <Br 'Fails12' '=' <Dg 'Fails12'> +
            ('SYNTAX_ERROR(12): ' +
            'Not exist symbol '']'' for symbol ''['' ' +
            'on the row: ' <Symb SR> ' and column: ' <Symb SC> '.')> +
            '['
    (']' SR SC) +
            = <DoTokenizerAux SR SC <Dg 'Fails12'>>
    (S('><+-.,')S SR SC) + 
            = SS 
    (SS SR SC) +
            =

DoTokenizerAux +
    SR SC   = <Br 'Fails11' '=' <Dg 'Fails11'> +
            ('SYNTAX_ERROR(11): ' +
            'Unexpected symbol '']'' ' +
            'on the row: ' <Symb SR> ' and column: ' <Symb SC> '.')>
    SR SC E2 (EF) +
            = <Br 'Fails12' '=' E2> ']'

IfTokenizer +
* Если Fails, то завершение программы
    'Fails' EE +
            = <Prout 'FATAL_ERROR (99): ' +'
            'There are one or more syntax errors.'> +
            <Map /DoProutLine/ EE>
* Запуск синтаксического анализатора 
    ET      = <Br 'e.ParsingTree' '=' +
            <Parser ET>> +
* Запуск интерпретатора
            <Prout 'The running of the interpretator ' +
            'of the code on the Brainfuck language.\n'> +
            <Interpretator <Dg 'e.ParsingTree'>>

DoRows +
    SR (EL) = <P1 SR> +
            <DelAccum <MapAccum /DoColumns/ (SR /1/) EL>>	
              
DoColumns +
    (SR SC) SS +
            = (SR <P1 SC>) +
            (SS SR SC)

DoProutLine +
    (EL)    = <Prout EL>

Go          = <Help> +
            <Prout 'The opening of the file "Test.b" ' +
            'with code on the Brainfuck language.\n'> +
* Загрузка строк файла в E.Lines := (E(O)L)*
            <Br 'E.Lines' '=' +
            <LoadFile /0/ 'Test.b'>> +
* Преобразование E.Lines в E.Symbols := (S(O)S S(N)R S(N)C)* 
            <Br 'E.Symbols' '=' +
            <DelAccum <MapAccum /DoRows/ /1/ <Dg 'E.Lines'>>>> +
* Запуск лексического анализатора
            <IfTokenizer <Tokenizer <Dg 'E.Symbols'>>>

            END