* Copyright 2025 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2025-04-10
* https://github.com/Aleksandr3Bocharov/RefalABBrainfuck

*====================================================================
* RefalABBrainfuck
*
* Программа "RefalABBrainfuck" написана
* на языке программирования РефалАБ.
* https://github.com/Aleksandr3Bocharov/RefalAB
*
* Интерпретатор языка программирования Brainfuck
* на РефалАБ.
*
* Программа "RefalABBrainfuck" интерпретирует 
* код на языке Brainfuck из исходного файла.
*====================================================================

RefalABBrainfuck +
    START
    ENTRY Go
    EXTRN Put_Char, Get_Char
    EXTRN Prout, M1, P1, Symb, Br, Dg
    EXTRN Map, Map_Accumulator, Del_Accumulator, Load_File, Exist_File
    EXTRN Platform, System, Arg, Exit
    EXTRN Windows, True, False
    EMPTY Symbols, Lines, Fails, Fails11, Fails12, Next, Prev, Inc, Dec, Write, Read

Go      = <chcp_UTF8 <Platform>> <Help> +
        <Args (<Arg /1/>) <Arg /2/>> +
* Преобразование E.Lines в E.Symbols := (S(O)S S(N)R S(N)C)* 
        <Br /Symbols/ '=' +
        <Del_Accumulator <Map_Accumulator /Do_Rows/ /1/ <Dg /Lines/>>>> +
* Запуск лексического анализатора
        <If_Tokenizer <Tokenizer <Dg /Symbols/>>>

* Смена кодовой страницы в Windows на UTF-8
chcp_UTF8
    /Windows/ +
        = <chcp_UTF8_Aux <System 'chcp 65001'>> 
    E0  =

chcp_UTF8_Aux
    E0  =

* Вывод справки
Help    = <Prout 'Интерпретатор языка ' +
        'программирования Brainfuck на РефалАБ.\n\n' +
        'Программа "RefalABBrainfuck" ' + 
        'интерпретирует код на языке Brainfuck ' +
        'из исходного файла.\n'>

Args    
    (E0) V1 +
        = <Args_Error>
    ()  = <Args_Error>
    (EF) +
        = <Open_File <Exist_File EF> EF>

Args_Error
        = <Prout 'FATAL_ERROR (9): ' +
        'Синтаксис - RefalABBrainfuck имя_файла'> +
        <Exit /9/>

Open_File
    /False/ EF +
        = <Prout 'FATAL_ERROR (19): ' +
        'Файл "' EF '" не существует.'> +
        <Exit /19/>
    /True/ EF +
        = <Prout 'Открытие исходного файла "' EF '" ' +
        'с кодом на языке программирования ' +
        'Brainfuck.\n'> +
* Загрузка строк файла в E.Lines := (E(O)L)*
        <Br /Lines/ '=' <Load_File /0/ EF>>

Do_Rows
    SR (EL) +
        = <P1 SR> +
        <Del_Accumulator <Map_Accumulator /Do_Columns/ (SR /1/) EL>>	
                      
Do_Columns
    (SR SC) SS +
        = (SR <P1 SC>) (SS SR SC)

If_Tokenizer
* Если Fails, то завершение программы
    /Fails/ EE +
        = <Prout 'FATAL_ERROR (99): ' +
        'Имеется одна или больше ' +
        'синтаксических ошибок.'> +
        <Map /Do_Prout_Line/ EE> +
        <Exit /99/>
* Запуск синтаксического анализатора 
    ET  = <Br 'e.ParsingTree' '=' <Parser ET>> +
* Запуск интерпретатора
        <Prout 'Запуск интерпретатора кода ' +
        'на языке программирования Brainfuck.\n'> +
        <Interpretator <Dg 'e.ParsingTree'>>

Do_Prout_Line
    (EL) +
        = <Prout EL>

* <Tokenizer (SS S(N)R S(N)C)*> == E('><+-.,[]')T | /Fails/ (V(O)E)+
Tokenizer
    ES  = <Tokenizer_Aux  <Map /Do_Tokenizer/ ES> (<Dg /Fails11/> <Dg /Fails12/>)>

Tokenizer_Aux
    ET () +
        = ET
    ET (EE) +
        = /Fails/ EE
  
Do_Tokenizer
    ('[' SR SC) +
        = <Br /Fails12/ '=' <Dg /Fails12/> +
        ('SYNTAX_ERROR(12): ' +
        'Не существует символа '']'' ' +
        'для символа ''['' ' +
        'на строке: ' <Symb SR> +
        ' и колонке: ' <Symb SC> '.')> +
        '['
    (']' E0) +
        = <Do_Tokenizer_Aux E0 <Dg /Fails12/>>
    (S('><+-.,')S E0) + 
        = SS 
    E0  =

Do_Tokenizer_Aux
    SR SC +
        = <Br /Fails11/ '=' <Dg /Fails11/> +
        ('SYNTAX_ERROR(11): ' +
        'Неожиданный символ '']'' ' +
        'на строке: ' <Symb SR> +
        ' и колонке: ' <Symb SC> '.')>
    SR SC E2 WF +
        = <Br /Fails12/ '=' E2> ']'

* <Parser E('><+-.,[]')T> == WN*
* WN ::= S(/Next/ /Prev/ /Inc/ /Dec/ /Write/ /Read/)L | (WN*)
Parser  =
    '>' ER +
        = /Next/ <Parser ER>
    '<' ER +
        = /Prev/ <Parser ER>
    '+' ER +
        = /Inc/ <Parser ER>
    '-' ER +
        = /Dec/ <Parser ER>
    '.' ER +
        = /Write/ <Parser ER>
    ',' ER +
        = /Read/ <Parser ER>
    '[' ER +
        = <Parser_Aux <Parser ER>>
    ']' ER +
        = (ER)      

Parser_Aux
    EP (ER) +
        = (EP) <Parser ER>

* <Interpretator WN*> == 
* WN ::= S(/Next/ /Prev/ /Inc/ /Dec/ /Write/ /Read/)L | (WN*)
Interpretator
    EP  = <Interpretator_Aux <Step_Interpretator EP () /0/ ()>>

Interpretator_Aux
    EC  = 

Step_Interpretator
    /Next/ ER (EB) SC () +
        = <Step_Interpretator ER (EB SC) /0/ ()>
    /Next/ ER (EB) SC (SN EA) +
        = <Step_Interpretator ER (EB SC) SN (EA)>   
    /Prev/ ER () SC (EA) +
        = <Step_Interpretator ER () /0/ (SC EA)>
    /Prev/ ER (EB SP) SC (EA) +
        = <Step_Interpretator ER (EB) SP (SC EA)>   
    /Inc/ E0 /255/ WA +
        = <Step_Interpretator E0 /0/ WA>
    /Inc/ E0 SC WA +
        = <Step_Interpretator E0 <P1 SC> WA>     							
    /Dec/ E0 /0/ WA +
        = <Step_Interpretator E0 /255/ WA>
    /Dec/ E0 SC WA +
        = <Step_Interpretator E0 <M1 SC> WA>   
    /Write/ E0 SC WA +
        = <Put_Char SC> +
        <Step_Interpretator E0 SC WA>												
    /Read/ E0 SC WA +
        = <Step_Interpretator E0 <Get_Char> WA>					
    WN ER WB /0/ WA +
        = <Step_Interpretator ER WB /0/ WA>
    (EN) ER WB SC WA +
        = <Step_Interpretator (EN) ER +
        <Step_Interpretator EN WB SC WA>>
    E0  = E0

    END
