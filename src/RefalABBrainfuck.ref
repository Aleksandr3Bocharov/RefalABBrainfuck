* Copyright 2025 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2025-09-05
* https://github.com/Aleksandr3Bocharov/RefalABBrainfuck

*====================================================================
* RefalABBrainfuck
*
* Программа "RefalABBrainfuck" написана
* на языке программирования РефалАБ.
* https://github.com/Aleksandr3Bocharov/RefalAB
*
* Интерпретатор языка программирования Brainfuck
* на РефалАБ.
*
* Программа "RefalABBrainfuck" интерпретирует 
* код на языке Brainfuck из исходного файла.
*====================================================================

RefalABBrainfuck +
    START

    ENTRY Go
    EXTRN Symbols, Tokenizer, Parser, Interpretator, Fails
    EXTRN Prouts, Br, Dg, Map, Load_File, Exist_File
    EXTRN Platform, System, Arg, Exit, Empty
    EXTRN Windows, True, False, NULL, FEOF, FError
    EMPTY Lines, Parsing_Tree

    IMPL

Go      = <chcp_UTF8 <Platform>> <Help> +
        <Args (<Arg 1>) <Arg 2>> +
* Запуск лексического анализатора
        <If_Tokenizer <Tokenizer <Symbols <Dg &Lines>>>>

* Смена кодовой страницы в Windows на UTF-8
chcp_UTF8
    &Windows +
        = <Empty <System 'chcp 65001'>> 
    E._ =

* Вывод справки
Help    = <Prouts 'Интерпретатор языка ' +
            'программирования Brainfuck на РефалАБ.\n\n' +
            'Программа "RefalABBrainfuck" ' + 
            'интерпретирует код на языке Brainfuck ' +
            'из исходного файла.\n'>

Args    
    (E._) V.__ +
        = <Args_Error>
    ()  = <Args_Error>
    (E.FileName) +
        = <Open_File <Exist_File E.FileName> E.FileName>

Args_Error
        = <Prouts 'FATAL_ERROR (9): ' +
            'Синтаксис - RefalABBrainfuck имя_файла'> +
        <Exit 9>

Open_File
    &False E.FileName +
        = <Prouts 'FATAL_ERROR (19): ' +
            'Исходный файл "' E.FileName '" не существует.'> +
        <Exit 19>
    &True E.FileName +
        = <Prouts 'Открытие исходного файла "' E.FileName '" ' +
            'с кодом на языке программирования ' +
            'Brainfuck.\n'> +
        <Open_File_Aux <Load_File 0 E.FileName>>

Open_File_Aux
    E.Open_Error &NULL +
        = <Prouts 'FATAL_ERROR (29): ' E.Open_Error> <Exit 29>
    E.Lines S(&FEOF &FError).EOF E._ +
        = <Br &Lines '=' E.Lines>

If_Tokenizer
* Если Fails, то завершение программы
    &Fails E.Errors +
        = <Prouts 'FATAL_ERROR (99): ' +
            'Имеется одна или больше ' +
            'синтаксических ошибок.'> +
        <Map &Do_Prout_Line E.Errors> +
        <Exit 99>
* Запуск синтаксического анализатора 
    E.Tokens +
        = <Br &Parsing_Tree '=' <Parser E.Tokens>> +
* Запуск интерпретатора
        <Prouts 'Запуск интерпретатора кода ' +
            'на языке программирования Brainfuck.'> +
        <Interpretator <Dg &Parsing_Tree>> <Prouts>

Do_Prout_Line
    (E.Line) +
        = <Prouts E.Line>

    END
